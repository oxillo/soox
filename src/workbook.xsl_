<xsl:package
  name="xlwriter:workbook"
  package-version="1.0"
  version="3.0"
  xmlns:xsl="http://www.w3.org/1999/XSL/Transform"
  xmlns:xs="http://www.w3.org/2001/XMLSchema"
  xmlns:xd="http://www.oxygenxml.com/ns/doc/xsl"
  xmlns:xlwfn="xlwriter-functions"
  xmlns:sooxns="simple-open-office-xml/namespaces"
  xmlns:xlwfile="xlwriter-files"
  xmlns:map="http://www.w3.org/2005/xpath-functions/map"
  xmlns:soox="simple-open-office-xml"
  xmlns:s="soox"
  exclude-result-prefixes="#all">
  
  <xsl:use-package name="xlwriter:utils" version="1.0"/>
  <xsl:use-package name="xlwriter:worksheet" version="1.0"/>
  <xsl:use-package name="soox:common" version="1.0"/>
  
  <xsl:include href="styles.xsl"/>
  
  <xsl:variable name="ns-sml" select="'http://schemas.openxmlformats.org/spreadsheetml/2006/main'"/>
  

  
  <xsl:function name="xlwfile:workbook.xml" visibility="public">
    <xsl:param name="simple_workbook"/>
    <xsl:param name="relationship-file"/>
    
    <xsl:variable name="relationships"
      select="$relationship-file('content')//*:Relationship[@Type = 'http://schemas.openxmlformats.org/officeDocument/2006/relationships/worksheet']"/>
    
    <xsl:variable name="content">
      <workbook xmlns="http://schemas.openxmlformats.org/spreadsheetml/2006/main"
        xmlns:r="http://schemas.openxmlformats.org/officeDocument/2006/relationships"
        xmlns:mc="http://schemas.openxmlformats.org/markup-compatibility/2006"
        mc:Ignorable="x15"
        xmlns:x15="http://schemas.microsoft.com/office/spreadsheetml/2010/11/main">
        
        <fileVersion appName="Calc"/>
        <workbookPr backupFile="false" showObjects="all" date1904="false"/>
        <workbookProtection/>
        <bookViews>
          <workbookView showHorizontalScroll="true" showVerticalScroll="true" showSheetTabs="true"
            xWindow="0" yWindow="0" windowWidth="16384" windowHeight="8192" tabRatio="500"
            firstSheet="0" activeTab="1"/>
        </bookViews>
        
        
        
        
        <!--fileVersion appName="xl" lastEdited="6" lowestEdited="6" rupBuild="14420"/>
        <workbookPr defaultThemeVersion="153222"/>
        <mc:AlternateContent
          xmlns:mc="http://schemas.openxmlformats.org/markup-compatibility/2006">
          <mc:Choice Requires="x15">
            <x15ac:absPath url="C:\Users\to24007\Dev\XSLT\Excel\"
              xmlns:x15ac="http://schemas.microsoft.com/office/spreadsheetml/2010/11/ac"
            />
          </mc:Choice>
        </mc:AlternateContent>
        <bookViews>
          <workbookView xWindow="0" yWindow="0" windowWidth="24300" windowHeight="14250"/>
        </bookViews-->
        <sheets>
          <xsl:for-each select="$simple_workbook/s:worksheet">
            <xsl:variable name="index" select="position()"/>
            <xsl:variable name="rId" select="$relationships[@Target=>ends-with($index||'.xml')]/@Id"/>
            <sheet name="{@name}" sheetId="{$index}" state="visible" r:id="{$rId}"/>
          </xsl:for-each>
        </sheets>
        <calcPr iterateCount="100" refMode="A1" iterate="false" iterateDelta="0.0001"/>
        <extLst>
          <ext xmlns:loext="http://schemas.libreoffice.org/"
            uri="{{7626C862-2A13-11E5-B345-FEFF819CDC9F}}">
            <loext:extCalcPr stringRefSyntax="ExcelA1"/>
          </ext>
        </extLst>
        
        <!--calcPr calcId="152511"/>
        <extLst>
          <ext uri="{{140A7094-0E35-4892-8432-C4D2E57EDEB5}}"
            xmlns:x15="http://schemas.microsoft.com/office/spreadsheetml/2010/11/main">
            <x15:workbookPr chartTrackingRefBase="1"/>
          </ext>
        </extLst-->
      </workbook>
    </xsl:variable>
    
    <xsl:sequence select="map{
      'content': $content,
      'types' : map{
      'content':'application/vnd.openxmlformats-officedocument.spreadsheetml.sheet.main+xml',
      'relationship': 'http://schemas.openxmlformats.org/officeDocument/2006/relationships/officeDocument'
      }}"/>
    
  </xsl:function>
  
  <xd:doc>
    <xd:desc>
      <xd:p>Generates the relationships file (workbook.xml.rels) from the map of previously generated files</xd:p>
    </xd:desc>
    <xd:param name="workbook-files">a map of files whose key is the file name. Each value should be a map with a 'types'->'relationship' key</xd:param>
  </xd:doc>
  <xsl:function name="xlwfile:workbook.xml.rels" visibility="public">
    <xsl:param name="workbook-files"/>
    
    <xsl:variable name="content">
      <xsl:element name="Relationships" namespace="{$sooxns:rels}">
        <xsl:for-each select="map:keys($workbook-files)">
          <xsl:sort select="current()"/>
          <xsl:element name="Relationship" namespace="{$sooxns:rels}">
            <xsl:attribute name="Id" select="'rId'||position()"/>
            <xsl:attribute name="Type" select="$workbook-files(current())('types')('relationship')"/>
            <xsl:attribute name="Target" select="substring-after(current(),'xl/')"/>
          </xsl:element>
        </xsl:for-each>
      </xsl:element>
    </xsl:variable>
    
    <xsl:sequence select="map{
      'content': $content,
      'types' : map{
        'content':'application/vnd.openxmlformats-package.relationships+xml'
      }}"/>
  </xsl:function>
  
  
  
  
  
  
</xsl:package>